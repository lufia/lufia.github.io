---
Title: Jenkinsfileの書き方
Category:
- Jenkins
- Exported from Qiita
Date: 2018-01-19T00:00:00+09:00
URL: https://blog.lufia.org/entry/2018/01/19/000000
EditURL: https://blog.hatena.ne.jp/lufiabb/lufia.hateblo.jp/atom/entry/26006613540501268
---

**この記事はQiitaで公開されていました**

ほとんど情報がなかったので調べながら書いたメモ。基本的に、公式情報が最もまとまっている。

- [Using a Jenkinsfile](https://jenkins.io/doc/book/pipeline/jenkinsfile/)
- [Pipeline Syntax](https://jenkins.io/doc/book/pipeline/syntax/)
- [Pipeline Steps Reference](https://jenkins.io/doc/pipeline/steps/)
- [Jenkins Job DSL API](https://jenkinsci.github.io/job-dsl-plugin/)

また、古い情報らしいけど、現状こちらにしか書かれていない項目もある。

- [pipeline-model-definition-plugin Wiki](https://github.com/jenkinsci/pipeline-model-definition-plugin/wiki)

Declarative Pipelineの1.2.8から、ジェネレータが追加されたようです。

- [The new things arriving in Declarative Pipeline!](https://jenkins.io/blog/2018/04/09/whats-in-declarative/)

## 基本

現状のJenkinsfileは2通り書き方があって、`pipeline`がルートになっている場合はDeclarative Pipelineという。この場合は、Groovyスクリプトを直接書くことができず、Groovyを書きたければ`script`ディレクティブを使う必要がある。この記事では主にこちらを扱う。

`pipeline`から始まらない場合、Scripted Pipelineといって、この場合は直接Groovyスクリプトも書けるし、`node()`や`stage()`などの、Pipeline Stepsメソッドも書くことができる。便利そうにみえるけど自由度が高すぎて職人コードになりがち。

## Declarative Pipelineの書き方

Jenkinsfileは特定の条件ごとにディレクティブを書いて実装する。ディレクティブの出現順は以下の順番になっている。例えば`stages`のすぐ下に`agent`を書くことはできない。

```groovy
pipeline {
	// 1行コメント
	/*
	 * 複数行コメント
	 */
	agent { ... }
	environment { ... }
	options {
		buildDiscarder(...)
		checkoutToSubdirectory(...)
		disableConcurrentBuilds(...)
		disableResume()
		newContainerPerStage(...)
		overrideIndexTriggers(...)
		preserveStashes(...)
		quietPeriod(...)
		retry(...)
		skipDefaultCheckout(...)
		skipStagesAfterUnstable(...)
		timeout(...)
		timestamps()
		parallelsAlwaysFailFast(...)
	}
	parameters {
		string(...)
		booleanParam(...)
		choice(...)
		file(...)
		text(...)
		password(...)
		run(...) // ??
	}
	tools {
		maven '...'
		jdk '...'
		gradle '...'
	}
	triggers {
		cron(...)
		pollSCM(...)
	}
	stages {
		stage {
			agent { ... }
			environment { ... }
			tools { ... }
			options {
				skipDefaultCheckout(...)
				timeout(...)
				retry(...)
				timestamps()
			}
			when { ... }
			steps {
				//Pipeline Steps Reference参照
				echo 'help'
				sh 'ls'
				script {
					// 任意のGroovyスクリプト
				}
			}
		}
	}
	steps {
		//Pipeline Steps Reference参照
	}
	post {
		always { ... }
		success { ... }
		failure { ... }
		...
	}
}
```

`steps`だけの場合は、直接`steps`の中に書くものを記述できる。

```groovy
echo 'help'
sh 'ls'
```

`node()`のように`steps`ディレクティブの中で子ブロックが現れる場合、そのブロックの中は`steps`ディレクティブと同じものが書ける。

```groovy
steps {
	node('slave1'){
		sh 'ls'
		dir('output'){
			sh 'ls'
		}
	}
}
```

## 個別の事例

### stageの書き方

`stage()`を使うと、進捗を分けて表示することができるが、インターネットでは2種類の書き方がある。`stage`単体で書かれているものは古く、推奨されない。

```
stage 'first'
sh 'ls'

stage 'second'
sh 'pwd'
```

今はブロックを取るようになっていて、こちらが推奨される。

```groovy
stage('first'){
	sh 'ls'
}
stage('second'){
	sh 'pwd'
}
```

### ビルドするノードを制限する

`agent`ディレクティブでラベルを指定する。

```groovy
agent {
	label 'slave1'
}
```

または、`steps`ディレクティブで`node`ブロックを使う。

```groovy
steps {
	node('slave1'){
	}
}
```

### 環境変数をセットする

`environment`ディレクティブが使える場合はその中で書く。ここでは、定数または`credentials()`しか使えない。また、`credentials()`はSSHユーザ名と秘密鍵を扱えない。

```groovy
environment {
	GOPATH = '/home/jenkins/go'

	// Jenkinsの資格情報に登録されている値を取り出す
	TOKEN = credentials('credential_id')
}
```

`credentials()`で取り出したものは、シークレットテキストの場合はそのまま使える。ユーザ名とパスワードの場合、`TOKEN`はユーザ名とパスワードを:で区切った文字列になっている。個別に使いたい場合は、`TOKEN_USR`や`TOKEN_PSW`のようにサフィックスを付けて扱うと良い。

または、`steps`ディレクティブの中で`withEnv`ブロックや`withCredentials`ブロックを使う。`withEnv`の中でだけ環境変数が有効になる。

```groovy
steps {
	withEnv(["GOPATH=${env.WORKSPACE}"]) {
	}
	withCredentials(bindings: [
		// シークレットテキスト
		string(credentialsId: 'credential_id', variable: 'TOKEN'),

		// ユーザ名とパスワード
		usernamePassword(credentialsId: 'credential_id',
			passwordVariable:'PASS',
			usernameVariable:'USER'),

		// ファイル
		file(credentialsId: 'credential_id', variable: 'FILE'),

		// SSH秘密鍵: passphraseVariable, usernameVariableは省略可
		sshUserPrivateKey(credentialsId: 'credential_id',
			keyFileVariable: 'KEY_FILE',
			passphraseVariable: '',
			usernameVariable: '')
	]){
	}
}
```

`withCredentials()`の詳細は[Credentials Binding Plugin](https://jenkins.io/doc/pipeline/steps/credentials-binding/)を読むと良い。

Groovyを使ってもっと細かく制御したい場合。`steps`ディレクティブに直接Groovyは書けないので`script`ディレクティブを使う。

```groovy
steps {
	script {
		// ここからGroovy
		env.GOPATH = env.WORKSPACE
	}
}
```

### ツールをインストールする

Jenkinsの管理メニューに**Global Tools Configuration** があり、そこで事前にMavenやGoコンパイラなどのバージョンと名前を定義しておくと、`tools`ディレクティブで、自動でインストールすることができる。

```groovy
pipeline {
	tools {
		go 'go1.10'
	}
}
```

**Pipeline Syntax**で自動生成したコードは`tool name: 'go1.9', type: 'go'`のように出力されるが、このまま*Declarative Pipeline*として記述するとエラーになる。`$type '$name'`のように置き換えて書く必要がある。

### パラメータ付きビルドにする

`parameters`に何か書くと、パラメータ付きビルドとして扱われる。`params.name`で参照する。

```groovy
pipeline {
	parameters {
		string(name: 'TARGET', defaultValue: 'Tests', description: '説明')
		choice(name: 'STAGE', choices: 'staging\nproduction', description: '説明')
	}
	stages {
		stage('build'){
			sh "./make.bash ${params.TARGET}"
		}
	}
}
```

### 実行条件を設定する

`stage()`ブロックで`when`ディレクティブを使う。以下の例は、ブランチ名が*release-\**にマッチした場合のみ*stage*を実行する。

```groovy
stage('stage'){
	when {
		branch 'release-*'
	}
}
```

他にも、環境変数の値を条件としたり、全て一致またはどれかが一致などの条件を作ることができる。

### 複数行のシェルスクリプトを実行

`'''`または`"""`で囲むと複数行のテキストを記述できる。

```groovy
steps {
	sh '''
	date
	sleep 10
	date
	'''
}
```

`'''`の場合は変数展開などをせずにそのままシェルへ渡す。`"""`なら`${env.PATH}`のような展開を行う。

### GitHub Branch Source Plugin環境でスレーブを使ってビルドする

基本的には何もしなくてもプラグインがcloneするが、デフォルトでは(おそらく)Jenkinsマスターのワークスペースへcloneされる。このため、ビルドノードがマスター以外の場合、ビルドするソースコードがスレーブのワークスペースにない状態となる。

`steps`ディレクティブで`checkout`を使うとslaveのワークスペースへcloneが行われる。

```groovy
steps {
	checkout scm
}
```

または最初から、`agent`ディレクティブでラベルを指定しておくと、対象となったスレーブでcloneされる。

```groovy
pipeline {
	agent {
		label 'slave1'
	}
	steps {
		sh 'ls'
	}
}
```

### 特定のディレクトリへcloneする

`options`ディレクティブで指定する。

```groovy
options {
	checkoutToSubdirectory 'src/v2'
}
```
または、`dir()`を使うとカレントディレクトリを変更できるので、移動した後で`checkout scm`を実行すればいい。移動するディレクトリが存在しない場合は自動的に作成される。

```groovy
stage('chekout'){
	steps {
		dir("src/v2"){
			checkout scm
		}
	}
}
```

### GitHub Branch Source Pluginでcheckoutの動作を変更する

[How to Customize Checkout for Pipeline Multibranch](https://support.cloudbees.com/hc/en-us/articles/226122247-How-to-Customize-Checkout-for-Pipeline-Multibranch)によると、`checkout scm`は、

```groovy
checkout([
	$class: 'GitSCM',
	branches: scm.branches,
	extensions: scm.extensions,
	userRemoteConfigs: scm.userRemoteConfigs
])
```

の省略形らしい。Shallow cloneとかcloneするディレクトリを変更する場合は、`scm.extensions`に配列でオプションを追加する。

```groovy
checkout([
	$class: 'GitSCM',
	branches: scm.branches,
	extensions: scm.extensions + [
		[ $class: 'CloneOption', noTags: false ],
		[ $class: 'RelativeTargetDirectory',
		  relativeTargetDir: "src/v2"
		],
	],
	userRemoteConfigs: scm.userRemoteConfigs
])
```

### Gitサブモジュールを使う

`checkout scm`に`SubmoduleOption`をセットします。

```groovy
checkout([
	$class: 'GitSCM',
	branches: scm.branches,
	extensions: scm.extensions + [
		[ $class: 'SubmoduleOption',
		  disableSubmodules: false,
		  parentCredentials: true,
		  recursiveSubmodules: true,
		  reference: '',
		  trackingSubmodules: false
		],
	],
	userRemoteConfigs: scm.userRemoteConfigs
])
```

ただし、GitHub Branch Source Pluginを使う場合、`parentCredentials`はGitHub APIトークンが使われているため、サブモジュールの参照もHTTPで行う必要があります。

### checkoutの前にワークスペースをクリアする

`checkout scm`の前に`deleteDir`を使う。

```groovy
stage('clean'){
	steps {
		deleteDir()
	}
}
stage('checkout'){
	steps {
		checkout scm
	}
}
```

### Scripts not permitted to use methodエラーで動作しない

Jenkinsfileのコードを実行した時、

> RejectedAccessException: Scripts not permitted to use method (メソッド名)

というエラーで停止する場合がある。これは、外部のコードを無条件に実行すると危険なので、[Script Security Plugin](https://wiki.jenkins-ci.org/display/JENKINS/Script+Security+Plugin)によってsandbox実行されているため、らしい。

外部のコードを制限するための機能なので、Jenkinsfileで回避できるものではない。エラーが発生した後であれば、Jenkinsの管理画面にエラーとなったメソッドの許可を求めるログが出ているので、そこでApprovalボタンを押せば次からはエラーを回避できる。このファイルは*$JENKINS_HOME/scriptApproval.xml*に置かれているので、これをコピーしてもいい。

- 参考: [Jenkinsfile を書く前に知っておくべきこと (セキュリティ制約編)](http://arasio.hatenablog.com/entry/2016/10/07/005055)

### 成果物を保存する

`post`セクションでArchive Artifact Pluginを使えばよい。

```groovy
pipeline {
	post {
		success {
			archiveArtifacts artifacts: bin/*, fingerprint: true
		}
	}
}
```

### 成果物の保存数を制限する

いろいろ書き方はあるが、おそらく`options`ディレクティブを使うのが簡単。

```groovy
pipeline {
    options {
        buildDiscarder(logRotator(numToKeepStr: '5', daysToKeepStr: '7', artifactNumToKeepStr: '5'))
    }
}
```

### 並列ビルドする

`steps`で`parallel`を使う。

```groovy
steps {
	parallel(
		linux: {
			sh './make.bash -t linux_amd64'
		},
		windows: {
			sh './make.bash -t windows_amd64'
		}
	)
}
```

この例では、`./make.bash -t linux_amd64`と`./make.bash -t windows_amd64`が並列実行される。

### 他のジョブをビルドする

`build`を使う。

引数のパラメータはJenkinsを置いているフォルダまでのパスを渡す。相対パスの場合は呼び出し元ジョブのディレクトリがカレントになり、絶対パスの場合は*$JENKINS_HOME/jobs*がルートになる。

```groovy
steps {
	build '../jobName1'
}
```

マルチブランチ構成のプロジェクトを呼び出す場合は、内部の階層がブランチごとに切られているので、ブランチ名も必要。

```groovy
steps {
	build '../jobName1/master'
}
```

GitHub Branch Sourceが管理しているジョブはマルチブランチに近くて、Organizationの下にジョブが作られるので、次のようになる。

```groovy
steps {
	build '/organizationName/jobs/jobName/master'
}
```

### Jenkinsが管理しているCredentialでssh接続したい

**ssh-agent**プラグインをインストールすると`sshagent`ブロックが利用可能になる。このブロックの中に記述したコマンドは、Jenkinsが管理している秘密鍵が追加された`ssh-agent`が動作してる状態で実行される。なのでJenkinsのアカウントで`git push`したい場合は、以下のように書く。

```groovy
steps {
	// jenkins_credential_id_strという名前のCredentialを読み込む
	sshagent(['jenkins_credential_id_str']){
		sh 'git push'
	}
}
```
